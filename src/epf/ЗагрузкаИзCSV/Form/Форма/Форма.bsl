

&НаКлиенте
Процедура ВыборФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

    ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
    ДиалогВыбораФайла.Фильтр                      = НСтр("ru='CSV файл (*.csv)|*.csv'");
    ДиалогВыбораФайла.Заголовок                   = НСтр("ru='Выберите CSV файл'");
    ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
    ДиалогВыбораФайла.Расширение                  = "csv";
    ДиалогВыбораФайла.ИндексФильтра               = 0;
    ДиалогВыбораФайла.ПолноеИмяФайла              = ПутьКФайлу;
    ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;

    Если ДиалогВыбораФайла.Выбрать() Тогда
    
    	ПутьКФайлу = ДиалогВыбораФайла.ПолноеИмяФайла;
        ПоказатьДанныеНаСервере();

    КонецЕсли;   

КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиИндексРеквизитаПоИмени(Имя, Реквизиты)
    Индекс = Реквизиты.Количество() - 1;
    Пока Индекс >= 0 Цикл
        Если Реквизиты[Индекс].Имя = Имя Тогда
       
            Возврат Индекс;
    	
        КонецЕсли;
        Индекс = Индекс - 1;
    КонецЦикла; 
    
    Возврат Неопределено;

КонецФункции

&НаСервере
Процедура ПоказатьДанныеНаСервере()
    Если Не ЗначениеЗаполнено(ПутьКФайлу) Тогда

    	Возврат;

    КонецЕсли; 
    
    #Область ЗаполнениеТаблицыЗначений
    
    ТЗ = Новый ТаблицаЗначений;
    НазванияКолонок = Новый Массив();
    Если ЗагрузитьНоменклатуру Тогда
        НазванияКолонок.Добавить("Наименование");
        НазванияКолонок.Добавить("Вид");
        НазванияКолонок.Добавить("Группа");
    Иначе
        НазванияКолонок.Добавить("Наименование");
        НазванияКолонок.Добавить("ПолноеЮридическоеНаименование");
        НазванияКолонок.Добавить("ИНН");
        НазванияКолонок.Добавить("ДатаРегистрации");
        НазванияКолонок.Добавить("Группа");
    КонецЕсли;
   
    Индекс = 0;
    Пока Индекс<НазванияКолонок.Количество() Цикл
      	ТЗ.Колонки.Добавить(НазванияКолонок[Индекс]);
        Индекс = Индекс+1;
    КонецЦикла; 
    
    Док = Новый ТекстовыйДокумент;
    Док.Прочитать(ПутьКФайлу);
    
    ЧислоСтрок = Док.КоличествоСтрок();
    ЧислоСтрок = ?(ЧислоСтрок>10, 10, ЧислоСтрок);
    Для Номер = 1 По ЧислоСтрок Цикл
    	Стр    = Док.ПолучитьСтроку(Номер);
        Массив = СтрРазделить(Стр,";");
        
        СтрТЗ = ТЗ.Добавить();
        Длина = Массив.Количество();
        ДлинаНазваний = НазванияКолонок.Количество();
        Длина = ?(Длина<ДлинаНазваний, Длина, ДлинаНазваний);
        Индекс=0;
        Пока Индекс<Длина Цикл
            СтрТЗ[НазванияКолонок[Индекс]]=Массив[Индекс];
            Индекс = Индекс+1;
        КонецЦикла; 
    КонецЦикла;
    
    #КонецОбласти 
    
    ИмяТаблицы = "ТаблицаCSV";
    
    #Область СозданиеРеквизитов
    
    ДобавляемыеРеквизиты = Новый Массив;
    
    #Область УдалениеСуществующихРеквизитов
    
    РеквизитыФормы = ПолучитьРеквизиты();
    Индекс = НайтиИндексРеквизитаПоИмени(ИмяТаблицы, РеквизитыФормы);
    Если Индекс <> Неопределено Тогда
        РеквизитТаблицы = РеквизитыФормы[Индекс];
        УдаляемыеПути = Новый Массив;
        УдаляемыеПути.Добавить(РеквизитТаблицы.Имя);
        ИзменитьРеквизиты(, УдаляемыеПути); 
    КонецЕсли;
    
    #КонецОбласти 
    
    ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы( ИмяТаблицы, Новый ОписаниеТипов("ТаблицаЗначений")));

    Для каждого Колонка Из ТЗ.Колонки Цикл
        
        Реквизит = Новый РеквизитФормы( Колонка.Имя, Колонка.ТипЗначения, ИмяТаблицы);
        ДобавляемыеРеквизиты.Добавить(Реквизит);
  
    КонецЦикла;
    ИзменитьРеквизиты(ДобавляемыеРеквизиты);
    
    #КонецОбласти 

    #Область СозданиеЭлементаФормы
    
    ЭлементТаблицы = Элементы.Найти("Таблица");
    
    #Область УдалениеСуществующейТаблицы
    
    Если ЭлементТаблицы <> Неопределено Тогда
        Элементы.Удалить(ЭлементТаблицы);
    КонецЕсли;
    
    #КонецОбласти 
    
    ТаблицаНаФорме = Элементы.Добавить("Таблица", Тип("ТаблицаФормы"));
    ТаблицаНаФорме.ПутьКДанным               = ИмяТаблицы;
    ТаблицаНаФорме.Отображение               = ОтображениеТаблицы.Список;
    ТаблицаНаФорме.Заголовок                 = "Предпросмотр первых строк";
    ТаблицаНаФорме.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Авто;
    ТаблицаНаФорме.КоманднаяПанель.Видимость = Ложь;
    
    Для Каждого Колонка Из ТЗ.Колонки Цикл

        ЭлементНаФорме = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), ТаблицаНаФорме);       
        ЭлементНаФорме.Вид         = ВидПоляФормы.ПолеНадписи;
        ЭлементНаФорме.ПутьКДанным = ИмяТаблицы + "." + Колонка.Имя;
        
    КонецЦикла;

    ЗначениеВРеквизитФормы(ТЗ, ИмяТаблицы);

    #КонецОбласти 

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере()
    Док = Новый ТекстовыйДокумент;
    Док.Прочитать(ПутьКФайлу);
    
    ДобавленоГрупп     = 0;
    ДобавленоЭлементов = 0;
    
    
    Для Номер = 1 По Док.КоличествоСтрок() Цикл
    
    	Стр    = Док.ПолучитьСтроку(Номер);
        Массив = СтрРазделить(Стр,";");
        
        Пока Массив.Количество() < ?(ЗагрузитьНоменклатуру, 3, 5) Цикл
        	Массив.Добавить("");
        КонецЦикла; 
        
        Если ЗагрузитьНоменклатуру Тогда
            Наименование = Массив[0];
            Вид          = Массив[1];
            Группа       = Массив[2];
            Справочник   = Справочники.Номенклатура;
        Иначе
            Наименование   = Массив[0];
            ЮрНаименование = Массив[1];
            ИНН            = Массив[2];
            ДатаРег        = Массив[3];
            Группа         = Массив[4];
            Справочник     = Справочники.Контрагенты;
        КонецЕсли;
        
        НовыйЭлемент   = Справочник.СоздатьЭлемент();
        
        #Область ПоискИСозданиеГруппыЭлемента
        
        Если ЗначениеЗаполнено(Группа) Тогда
        
        	Категория = Справочник.НайтиПоНаименованию(Группа);
            Если Категория = Справочник.ПустаяСсылка() Тогда
            
            	Категория = Справочник.СоздатьГруппу();
                Попытка
                    Категория.Наименование = Группа;
                    Категория.Записать();
                    ДобавленоГрупп = ДобавленоГрупп + 1;
                Исключение
                    Сообщить("Не удалось создать группу """ + Категория + """!
                                   |" + ОписаниеОшибки());
                КонецПопытки;
            КонецЕсли; 
            НовыйЭлемент.Родитель = Категория.Ссылка;
        
        КонецЕсли; 
        
        #КонецОбласти 
        
        Попытка
            НовыйЭлемент.Наименование = Наименование;
            
            Если ЗагрузитьНоменклатуру Тогда
                Если      НРег(Вид) = "товар"  Тогда
                	НовыйЭлемент.ВидТовара = Перечисления.ВидТовара.Товар;
                ИначеЕсли НРег(Вид) = "услуга" Тогда
                	НовыйЭлемент.ВидТовара = Перечисления.ВидТовара.Услуга;
                Иначе //  Вызов ошибки
                    
                    ВызватьИсключение "Вид номенклатуры должен быть товар или услуга";
                    
                КонецЕсли;     
            Иначе
                НовыйЭлемент.ПолноеЮридическоеНаименование = ЮрНаименование;
                НовыйЭлемент.ИНН                           = Число(ИНН);
                НовыйЭлемент.ДатаРегистрации               = Дата(ДатаРег);
            КонецЕсли;
        
            НовыйЭлемент.Записать();
            ДобавленоЭлементов = ДобавленоЭлементов + 1;
        Исключение 
            Сообщить("Не удалось записать элемент """ + НовыйЭлемент + """!
                           |" + ОписаниеОшибки());
        КонецПопытки; 
    КонецЦикла;
    Если ДобавленоЭлементов>0 ИЛИ ДобавленоГрупп>0 Тогда
    	Сообщить("Добавлено " + ?(ДобавленоЭлементов, "элементов - " + ДобавленоЭлементов, "") +
              ?(ДобавленоГрупп, ?(ДобавленоЭлементов, ", " ,"") + "групп - " + ДобавленоГрупп, ""));
    КонецЕсли;   
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
    ЗагрузитьНаСервере();
    ОбновитьИнтерфейс();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    Если ЗагрузитьНоменклатуру = ЗагрузитьКонтрагентов Тогда
    	ЗагрузитьНоменклатуру = Не ЗагрузитьКонтрагентов;
    КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ТипДанныхПриИзменении(Элемент)
    Если Элемент.Имя = "ЗагрузитьНоменклатуру" Тогда
    	ЗагрузитьКонтрагентов = Не ЗагрузитьНоменклатуру;
    Иначе    
    	ЗагрузитьНоменклатуру = Не ЗагрузитьКонтрагентов;
    КонецЕсли;
    ПоказатьДанныеНаСервере(); 
КонецПроцедуры
